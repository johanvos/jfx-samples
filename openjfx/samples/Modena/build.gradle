/*
 * Copyright (c) 2018, OpenJFX and/or its affiliates.
 * All rights reserved. Use is subject to license terms.
 *
 * This file is available and licensed under the following license:
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions
 * are met:
 *
 *  - Redistributions of source code must retain the above copyright
 *    notice, this list of conditions and the following disclaimer.
 *  - Redistributions in binary form must reproduce the above copyright
 *    notice, this list of conditions and the following disclaimer in
 *    the documentation and/or other materials provided with the distribution.
 *  - Neither the name of OpenJFX nor the names of its
 *    contributors may be used to endorse or promote products derived
 *    from this software without specific prior written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
 * "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
 * LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
 * A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT
 * OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
 * SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT
 * LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
 * DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
 * THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
 * (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
 * OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 */
apply plugin:'application'

repositories {
    mavenCentral()
}

def getOSName() {
    final String osName = System.getProperty("os.name").toLowerCase();
    if (osName.contains("linux")) {
        return ("linux");
    } else if (osName.contains("mac os x") || osName.contains("darwin") || osName.contains("osx")) {
        return ("mac");
    } else if (osName.contains("windows")) {
        return ("win");
    }
    return ("");
}

ext.platform = getOSName()

mainClassName = "modena.Modena"

dependencies {
    compile "org.openjfx:javafx-controls:11-ea+19:${platform}"
    compile "org.openjfx:javafx-graphics:11-ea+19:${platform}"
    compile "org.openjfx:javafx-base:11-ea+19:${platform}"
    compile "org.openjfx:javafx-fxml:11-ea+19:${platform}"
    compile "org.openjfx:javafx-web:11-ea+19:${platform}"
    compile "org.openjfx:javafx-media:11-ea+19:${platform}"
    compile "org.openjfx:javafx-swing:11-ea+19:${platform}"
}

jar {
    manifest.attributes("Main-Class": mainClassName)
}

def excludedArtifacts = configurations.compile.resolvedConfiguration.resolvedArtifacts.findAll {
    it.moduleVersion.id.group == 'org.openjfx' && it.classifier != platform
}

compileJava {
    doFirst {
        options.compilerArgs = [
            '--module-path', classpath.asPath,
            '--add-modules', 'javafx.controls,javafx.fxml,javafx.swing,javafx.web'
        ]
        classpath = files()
    }
}

run {
    doFirst {
        classpath -= files(excludedArtifacts.collect { it.file })
        jvmArgs = [
            '--module-path', classpath.asPath,
            '--add-modules', 'javafx.controls,javafx.fxml,javafx.swing,javafx.web'
        ]
        classpath = sourceSets.main.output
    }
}
